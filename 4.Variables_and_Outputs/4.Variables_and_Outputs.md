# Variables and Outputs

The Terraform language includes a few kinds of blocks for requesting or publishing named values.

## Input Variables

Input Variables serve as parameters for a Terraform module, so users can customize behavior without editing the source. To make your Terraform configuration more flexible and reusable, you can use input variables. This allows you to parameterize your Terraform code, making it easier to manage and adapt to different environments or requirements.

### Defining an Input Variable

Here's how you define an input variable:

```hcl
variable "example_var" {
  description = "An example input variable"
  type        = string
  default     = "default_value"
}
```
In this example:

- `variable` is used to declare an input variable named example_var.
- `description` offers a human-readable explanation of the variable.
- `type` defines the data type of the variable (e.g., string, number, list, map, etc.).
- `default` specifies an optional default value for the variable.

### Using an Input Variable
You can then use the input variable within your module or configuration like this:
You reference the input variable using var.example_var.

```hcl
resource "example_resource" "example" {
  name = var.example_var
}
```

### Example
#### variables.tf
```hcl
variable "aws_region" {
  description = "The AWS region to deploy in"
  type        = string
  default     = "us-west-2"
}

variable "ami_id" {
  description = "The AMI ID to use for the instance"
  type        = string
  default     = "ami-0b20a6f09484773af" # Specify your desired AMI ID
}

variable "instance_type" {
  description = "The type of instance to create"
  type        = string
  default     = "t2.micro"
}

variable "key_name" {
  description = "The name of the key pair to use"
  type        = string
  default     = "demo_config_key_us-west-2"
}

variable "security_group_id" {
  description = "The ID of the security group to assign to the instance"
  type        = string
  default     = "sg-00f1906a3bb9a9383"
}

variable "subnet_id" {
  description = "The ID of the subnet to launch the instance in"
  type        = string
  default     = "subnet-c6b4b7bf"
}

variable "instance_name" {
  description = "The name to tag the instance with"
  type        = string
  default     = "ExampleInstance"
}

```

#### main.tf
```hcl
terraform {
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "~> 5.0"
    }
  }
}

provider "aws" {
  region = var.aws_region
}

# Create an EC2 instance
resource "aws_instance" "example" {
  ami                    = var.ami_id
  instance_type          = var.instance_type
  key_name               = var.key_name
  vpc_security_group_ids = [ var.security_group_id ]
  subnet_id              = var.subnet_id
  user_data              = <<-EOF
                #!/bin/bash
                yum update -y
                yum install -y httpd
                systemctl start httpd
                systemctl enable httpd
                echo "<html><body><h1>Hi Utsav! Welcome to your EC2 instance!</h1></body></html>" > /var/www/html/index.html
                EOF
  tags = {
    Name = var.instance_name
  }
}

```



## Output Values

Output Values are like return values for a Terraform module. Terraform output is a feature that allows you to query and extract data. This can be useful for obtaining information about the resources that Terraform has created or for passing data between different Terraform configurations. Outputs are defined in your Terraform configuration files using the output block.

### Defining an Output Variable
Here's how you define an output variable:

```hcl
output "example_output" {
  description = "An example output variable"
  value       = resource.example_resource.example.id
}
```

In this example:
- `output` is used to declare an output named example_output.
- `description` provides a human-readable description of the output.
- `value` specifies the value to be output. This can be any Terraform expression.

### Example
#### outputs.tf
```hcl
output "instance_id" {
  description = "The ID of the EC2 instance"
  value       = aws_instance.example.id
}

output "instance_public_ip" {
  description = "The public IP address of the EC2 instance"
  value       = aws_instance.example.public_ip
}
```

## .tfvars file

Terraform allows you to define variable files called *.tfvars to create a reusable file for all the variables for a project.

In Terraform, .tfvars files, usually identified by their .tfvars extension, are utilized to assign particular values to input variables outlined in your Terraform configuration. These files enable the segregation of configuration values from your Terraform code, simplifying the management of diverse configurations for various environments (like development, staging, and production) or facilitating the storage of sensitive information without its exposure in your code.

#### Here's a basic example of what a .tfvars file might look like:
```hcl
aws_region = "us-west-2"
ami_id = "ami-0b20a6f09484773af"
instance_type = "t2.micro"
key_name = "demo_config_key_us"
security_group_id = "sg-00f1906a3bb9a9383"
subnet_id = "subnet-c6b4b7bf"
instance_name = "ExampleInstance"
```

In this example, aws_region, ami_id, instance_type,  key_name, security_group_id, subnet_id, and instance_name are variables that might be used in a Terraform configuration. By defining these values in a .tfvars file, you can easily reuse this file across different environments or configurations.

#### When you apply your Terraform configuration, you can specify the .tfvars file using the -var-file flag:
```powershell
terraform apply -var-file=terraform.tfvars
```

### Here's why `.tfvars` files are used:

**Separation of Configuration and Code:** Terraform's input variables are designed to be customizable, allowing you to use the same code with various value sets. Instead of embedding these values directly into your `.tf` files, you use `.tfvars` files to maintain a separation. This simplifies the task of managing configurations for different environments.

 **Storage of Sensitive Information:** `.tfvars` files serve as a secure repository for sensitive information such as API keys, access credentials, or other secrets. These values can be kept outside version control systems, bolstering security and preventing inadvertent exposure of secrets in your codebase.

**Enhanced Reusability:** By storing configuration values in separate `.tfvars` files, you can reuse the same Terraform code with different variable sets. This is particularly beneficial for deploying infrastructure for distinct projects or environments using a single set of Terraform modules.

**Facilitates Collaboration:** In team environments, each team member can utilize their own `.tfvars` file to set environment or workflow-specific values. This practice helps avoid conflicts within the codebase when multiple individuals are working on the same Terraform project.